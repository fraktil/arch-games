# Contributor: Anton Bazhenov <anton.bazhenov at gmail>
# Contributor: Daniel J Griffiths <ghost1227@archlinux.us>

pkgname=openlierox
pkgver=0.57_rc1
pkgrel=1
pkgdesc="A real-time excessive Worms-clone"
arch=('i686' 'x86_64')
url="http://www.openlierox.net/"
license=('LGPL')
depends=('gcc-libs' 'sdl_image' 'sdl_mixer' 'gd' 'hawknl' 'libxml2' 'libzip')
optdepends=('bash: for scripts support'
            'python: for scripts support')
conflicts=('openlierox-beta' 'openlierox-svn')
source=(http://downloads.sourceforge.net/$pkgname/OpenLieroX_$pkgver.src.tar.bz2)
md5sums=('2c31eee74f21f8d2907d14e31a7617d3')

build() {
  cd "$srcdir"/OpenLieroX

  # compile
  sed -i "s/grep_param()/grep_param/" compile.sh
  ./compile.sh || return 1

  # install binary
  install -Dm755 bin/$pkgname "$pkgdir"/usr/bin/$pkgname

  # install data
  install -m755 -d "$pkgdir"/usr/share/OpenLieroX
  cp -r share/gamedir/* "$pkgdir"/usr/share/OpenLieroX/
  find "$pkgdir"/usr/share/OpenLieroX -type d -exec chmod 755 {} +
  find "$pkgdir"/usr/share/OpenLieroX -type f -exec chmod 644 {} +

  # install manual
  install -Dm644 doc/$pkgname.6 "$pkgdir"/usr/share/man/man6/$pkgname.6

  # install icon
  install -Dm644 share/OpenLieroX.svg \
    "$pkgdir"/usr/share/pixmaps/OpenLieroX.svg

  # install .desktop file
  install -Dm644 share/$pkgname-$pkgname.desktop \
    "$pkgdir"/usr/share/applications/$pkgname.desktop
}
