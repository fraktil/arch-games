# Maintainer: Arkham <arkham at archlinux dot us>
# Contributor: Marcelo Cavalcante / Kalib <kalib@archlinux-br.org>
# Contributor: Matthias Maennich <arch@maennich.net>
# Contributor: Andrea Scarpino <bash.lnx@gmail.com>
# Contributor: Darwin Bautista <djclue917@gmail.com>

pkgname=planeshift
pkgver=0.4.03
pkgrel=2
pkgdesc="Open source and cross-platform 3D Fantasy MMORPG"
arch=('i686' 'x86_64')
_arch="x86"
[ "${CARCH}" = "x86_64" ] && _arch="x64"
url="http://www.planeshift.it/"
license=('GPL' 'custom:PlaneShift Content License')
depends=( 'mesa' 'openal' 'speex' 'libvorbis' 'alsa-lib' 'libpng' 'libmng')
options=('emptydirs')
install=$pkgname.install
source=(http://psmirror.org/PlaneShift-v${pkgver}-${_arch}.bin
    	planeshift.desktop
	    planeshiftsetup.desktop
    	planeshiftupdater.desktop
    	license.txt
    	scriptfix.patch)
md5sums=('bfa294eed54d477dff65739bc4b45349'
         '5be27f6e51c8edff591aa1fdc668b7e9'
         'e75381f677b5d2ae331d0e4e742ee21a'
         '4e8d4876b9c99db92d6dcb3e0c22128e'
         'dbac9cf5bfb63463bcb2b5ea09b00586'
         '52294da6a00190be91c32244c0700643')
[ "${CARCH}" = "x86_64" ] &&
    md5sums[0]='7412c12e164eab2de5a320b0da0ce512'

build() {
    cd $srcdir

    # Make the install executable
    chmod +x PlaneShift-v${pkgver}-${_arch}.bin

    # Extract the files
    ./PlaneShift-v${pkgver}-${_arch}.bin \
        --mode unattended \
        --syswide yes \
        --prefix $pkgdir/opt

    # Set correct permissions for files and folders
    find $pkgdir/opt/PlaneShift -type f -exec chmod 664 {} +
    find $pkgdir/opt/PlaneShift -type d -exec chmod 775 {} +
    chmod -R root:root $pkgdir/opt

    cd $pkgdir/opt/PlaneShift

    # Set correct permissions for executables
    chmod 775 psclient.bin pssetup.bin psupdater.bin

    # Install scripts to proper directory
    install -Dm 755 psclient $pkgdir/usr/bin/$pkgname
    install -Dm 755 pssetup $pkgdir/usr/bin/$pkgname-setup
    install -Dm 755 psupdater $pkgdir/usr/bin/$pkgname-updater

    # Remove innecessary files
    rm uninstall unscript.sh $pkgname $pkgname-setup $pkgname-updater psclient pssetup psupdater

    cd $pkgdir/usr/bin/

    #fix script
    patch -Np0 -i $srcdir/scriptfix.patch || return 1

    # Install shortcuts
    mkdir -p $pkgdir/usr/share/applications
    install -Dm 644 $srcdir/*.desktop $pkgdir/usr/share/applications/

    # Install the PlaneShift Content License
    install -Dm 644 $srcdir/license.txt $pkgdir/usr/share/licenses/$pkgname/LICENSE
}
