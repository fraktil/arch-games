# Contributor: [vEX] <niechift.dot.vex.at.gmail.dot.com>
pkgname=bsnes
pkgver=0.063
pkgrel=1
pkgdesc="Super Nintendo Entertainment System (SNES) emulator focused on accuracy."
arch=('i686' 'x86_64')
url="http://byuu.org/bsnes/"
license=('GPL2' 'LGPL' 'zlib' 'custom:unrar')
depends=('freeglut' 'libao' 'libgl' 'libxv' 'openal' 'sdl' 'qt>=4.5.0')
makedepends=('pkgconfig')
replaces=('snesfilter' 'snesreader' 'supergameboy')
source=("http://bsnes.googlecode.com/files/${pkgname}_v063.tar.bz2")
md5sums=('f84892d7e44451c2807308c3135936cf')

build() {
  cd "${srcdir}/src"

  # This _might_ help gaining some extra speed by allowing processor specific
  # optimizations, (ie SSE/SSE2), needs gcc >= 4.2.0
  GCC_VER=`gcc --version | grep gcc | cut -d ' ' -f 3`
  GCC_MAJOR=`echo $GCC_VER | cut -d '.' -f 1`
  GCC_MINOR=`echo $GCC_VER | cut -d '.' -f 2`
  if [ $GCC_MAJOR -ge 4 -a $GCC_MINOR -ge 2 ]; then
    msg "gcc version greater than 4.2 found, enabling -mtune=native optimizations"
    sed -e 's|flags := -O3 |flags := -O3 -mtune=native |' \
        -i "${srcdir}/src/Makefile" || return 1
  fi

  # Makefile hacks
  # If the help message is printed something went wrong and it should exit with
  # an error code
  echo "	@exit 1" >> $srcdir/src/Makefile
  # Disable pulseaudio.
  sed -e 's|audio.pulseaudio ||' \
      -e 's|audio.pulseaudiosimple ||' \
      -i "${srcdir}/src/Makefile" || return 1

  # Build and install it
  make || return 1
  make install DESTDIR=$pkgdir prefix=/usr || return 1
  
  ##
  # Now lets build and install the plugins, you can remove any of the below if 
  # you don't want/need them.
  ##
  # snesfilter :: output filters
  ##
  msg "Building snesfilter plugin"
  cd "${srcdir}/snesfilter"
  sed -e 's|\(libsnesfilter.*\) $(DESTDIR)$(prefix)/lib|\1 $(DESTDIR)$(prefix)/lib/\1|' \
      -i "${srcdir}/snesfilter/Makefile" || return 1
  make || return 1
  make install DESTDIR=$pkgdir prefix=/usr || return 1
  chmod 644 "${pkgdir}/usr/lib/libsnesfilter.a" || return 1
  ##
  # snesreader :: handle compressed files
  ##
  msg "Building snesreader plugin"
  cd "${srcdir}/snesreader"
  sed -e 's|\(libsnesreader.*\) $(DESTDIR)$(prefix)/lib|\1 $(DESTDIR)$(prefix)/lib/\1|' \
      -i "${srcdir}/snesreader/Makefile" || return 1
  make || return 1
  make install DESTDIR=$pkgdir prefix=/usr || return 1
  chmod 644 "${pkgdir}/usr/lib/libsnesreader.a" || return 1
  # Licenses
  mkdir -p "${pkgdir}/usr/share/licenses/bsnes"
  install -m644 "${srcdir}/snesreader/7z_C/lzma.txt" "${pkgdir}/usr/share/licenses/bsnes/license-7zip.txt"
  install -m644 "${srcdir}/snesreader/unrar/license.txt" "${pkgdir}/usr/share/licenses/bsnes/license-unrar.txt"
  ##
  # supergameboy :: Super Game Boy support
  ##
  msg "Building supergameboy plugin"
  cd "${srcdir}/supergameboy"
  sed -e 's|\(libsupergameboy.*\) $(DESTDIR)$(prefix)/lib|\1 $(DESTDIR)$(prefix)/lib/\1|' \
      -i "${srcdir}/supergameboy/Makefile" || return 1
  make || return 1
  make install DESTDIR=$pkgdir prefix=/usr || return 1
  chmod 644 "${pkgdir}/usr/lib/libsupergameboy.a" || return 1
}
